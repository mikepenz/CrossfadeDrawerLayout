apply plugin: 'com.android.application'
//wrap with try and catch so the build is working even if the signing stuff is missing
try {
    apply from: '../../../signing.gradle'
} catch (ex) {
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 23
        versionCode 31
        versionName '0.3.1'

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def file = output.outputFile
                def fileName = file.name.replace(".apk", "-v" + versionName + "-c" + versionCode + ".apk")
                output.outputFile = new File(file.parentFile, fileName)
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            try {
                signingConfig signingConfigs.debug
            } catch (ex) {
            }
            minifyEnabled false
        }
        release {
            try {
                signingConfig signingConfigs.release
            } catch (ex) {
            }
            zipAlignEnabled true
            minifyEnabled false
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile project(':library')

    // used to provide the fast, flexible, and easy to use DrawerLayout which follows the Material Design Guidelines
    // https://github.com/mikepenz/MaterialDrawer
    compile('com.mikepenz:materialdrawer:5.0.6-SNAPSHOT@aar') {
        transitive = true
    }

    // used to generate the Open Source section
    // https://github.com/mikepenz/AboutLibraries
    compile('com.mikepenz:aboutlibraries:5.5.7@aar') {
        transitive = true
        exclude module: "fastadapter"
    }

    // icon fonts used inside the sample
    // https://github.com/mikepenz/Android-Iconics
    compile 'com.mikepenz:material-design-iconic-typeface:2.2.0.1@aar'
    compile 'com.mikepenz:fontawesome-typeface:4.4.0.1@aar'

    // used to showcase how to load images
    compile 'com.squareup.picasso:picasso:2.5.2'
}
